/-
Copyright (c) 2024 TheLeanTeam. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: The Lean Team
-/
import Mathlib.Analysis.InnerProductSpace.Adjoint

/-!
 # Week 7 - Diamonds

 In this file, we will learn how to identify and resolve diamonds (type mismatch errors).

 **Diamonds** are when we have the following type inheritance diagram:
    D
   / \
  B   C
   \ /
    A
  i.e., a diamond is when we have `D` being inferred by both `B` and `C` which are inferred by `A`.

 We call a diamond **transparent** when `D` (inferred by `B`) is definitionally equal to
  `D` (inferred by `C`). We also sometimes say that a diamond is "resolved" when there are no
  non-transparent sub-diamonds (i.e., diamonds within the diamond). In other words, when the error
  is resolved.
-/

section type_inheritance_diagram_example

/-
 type `A` with instance `B : Ring A` and `C : AddCommGroup A` means we have the following type inheritance diagram:

  B   C
   \ /
    A
-/
variable {A : Type} [B : Ring A] [C : AddCommGroup A]

-- now let `D` be `AddCommMonoid A`, and we know that this is inferred by `B` and `C`:
#check B.toAddCommMonoid
#check C.toAddCommMonoid

/-
 so then we have a diamond inheritance:
    D
   / \
  B   C
   \ /
    A
-/

end type_inheritance_diagram_example

/- `NormedAddCommGroup`, `InnerProductSpace`, `Ring`, `Algebra` and `Finite_dimensional`
  gives a mismatch error when trying to deal with linear maps. -/
-- example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
--   [hE‚ÇÉ : Ring E] [hE‚ÇÑ : Algebra ‚ÑÇ E] (T : E ‚Üí‚Çó[‚ÑÇ] E) [hE‚ÇÖ : FiniteDimensional ‚ÑÇ E] :
--   LinearMap.adjoint T = T :=
-- sorry

section naive_approach

/-!
 ## Fixing the error: the naive approach
-/

-- can we be more specific to fix the error?
-- example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
--   [hE‚ÇÉ : Ring E] [hE‚ÇÑ : Algebra ‚ÑÇ E] (T : E ‚Üí‚Çó[‚ÑÇ] E) [hE‚ÇÖ : FiniteDimensional ‚ÑÇ E] :
--   @LinearMap.adjoint ‚ÑÇ E E Complex.instIsROrCComplex hE‚ÇÅ hE‚ÇÅ hE‚ÇÇ hE‚ÇÇ hE‚ÇÖ hE‚ÇÖ T = T :=
-- sorry

/- nope
  ... so how do we fix this?!

  according to the Infoview,
  there seems to be two "non-transparent diamonds".
  the first `‚óá` is `AddCommGroup.toAddCommMonoid` clashing with
  `NonUnitalNonAssocSemiring.toAddCommMonoid`
  this is because the following are not definitionally equal: -/
-- example {E : Type*} [hE : NormedAddCommGroup E] [hE‚ÇÇ : Ring E] :
--   hE.toAddCommMonoid = hE‚ÇÇ.toAddCommMonoid :=
-- rfl -- gives an error

-- can we specify which instances the linear map should access?
-- example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
--   [hE‚ÇÉ : Ring E] [hE‚ÇÑ : Algebra ‚ÑÇ E] (T : E ‚Üí‚Çó[‚ÑÇ] E) [hE‚ÇÖ : FiniteDimensional ‚ÑÇ E] :
--   LinearMap.adjoint (T : @LinearMap ‚ÑÇ ‚ÑÇ _ _ _ E E
--     (hE‚ÇÅ.toAddCommMonoid) (hE‚ÇÅ.toAddCommMonoid)
--     (NormedSpace.toModule) (NormedSpace.toModule)) = T := sorry

-- nope...
-- can we fix this by reordering the instances?
example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  (T : E ‚Üí‚Çó[‚ÑÇ] E) [Ring E] [Algebra ‚ÑÇ E] [hE : FiniteDimensional ‚ÑÇ E] :
  LinearMap.adjoint T = T := sorry

-- yes!

-- BUT, what if we needed to define a linear map in the proof?
example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  (T : E ‚Üí‚Çó[‚ÑÇ] E) [Ring E] [Algebra ‚ÑÇ E] [hE : FiniteDimensional ‚ÑÇ E] :
  LinearMap.adjoint T = T := by
{
  -- let f : E ‚Üí‚Çó[‚ÑÇ] E := sorry
  -- have : @LinearMap.adjoint ‚ÑÇ E E _ hE‚ÇÅ hE‚ÇÅ hE‚ÇÇ hE‚ÇÇ hE hE f = 0 := sorry
  -- back to the error...

  -- one way to fix this is by specifying the function:
  let f' : @LinearMap ‚ÑÇ ‚ÑÇ _ _ (RingHom.id ‚ÑÇ) E E (NormedAddCommGroup.toAddCommGroup.toAddCommMonoid)
    (NormedAddCommGroup.toAddCommGroup.toAddCommMonoid)
    (NormedSpace.toModule) (NormedSpace.toModule) := sorry
  have this' : LinearMap.adjoint f' = 0 := sorry

  -- this works fine BUT there must be a better way to handle this error than to do this
  -- every time??

  sorry
}

end naive_approach

section resolving_diamonds

/-!
 ## Resolving diamonds
-/

/-
 We already concluded previously that there are non-transparent diamonds within our example.

 The first is `NormedAddCommGroup` clashing with `Ring`.
-/

/- note that we have the following transparent diamond `‚óá`,
  i.e., `AddCommMonoid` created by `Ring ‚Üí AddCommGroup` is definitionally equal to that
  created by `Ring ‚Üí NonUnitalNonAssocSemiring ‚Üí AddCommMonoid`. -/
example {E : Type*} [h : Ring E] :
  ((h.toAddCommGroup).toAddCommMonoid : AddCommMonoid E) = h.toAddCommMonoid :=
rfl

/-- `NormedAddCommGroup` structure with `Ring`, i.e., the properties of `Ring`
  and `NormedAddCommGroup`, but without the common property `AddCommGroup`. -/
@[class, reducible] structure NormedAddCommGroup_ofRing (E : Type*)
  extends Norm E, Ring E, MetricSpace E where
  dist := fun x y => ‚Äñx - y‚Äñ
  /-- The distance function is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = ‚Äñx - y‚Äñ := by aesop

instance NormedAddCommGroup_ofRing.inst_Ring {E : Type*} [h : NormedAddCommGroup_ofRing E] :
  Ring E :=
h.toRing
instance NormedAddCommGroup_ofRing.inst_NormedAddCommGroup {E : Type*}
  [h : NormedAddCommGroup_ofRing E] :
  NormedAddCommGroup E :=
{ toNorm := h.toNorm
  toAddCommGroup := h.toAddCommGroup
  toMetricSpace := h.toMetricSpace
  dist_eq := h.dist_eq }

/-
 Let's check that we now get the same `AddCommMonoid`:
-/
example {E : Type*} [h : NormedAddCommGroup_ofRing E] :
  h.toAddCommMonoid = NormedAddCommGroup.toAddCommGroup.toAddCommMonoid :=
rfl

/-
 We also get the same `AddCommGroup` structure:
-/
example {E : Type*} [NormedAddCommGroup_ofRing E] :
  (NormedAddCommGroup.toAddCommGroup : AddCommGroup E) = Ring.toAddCommGroup :=
rfl

/- we have resolved the first diamond! -/

-- this now works!!!
example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup_ofRing E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  [Algebra ‚ÑÇ E] [FiniteDimensional ‚ÑÇ E] (T : E ‚Üí‚Çó[‚ÑÇ] E) :
  LinearMap.adjoint T = 0 := by
{
  let f : E ‚Üí‚Çó[‚ÑÇ] E := sorry
  have : LinearMap.adjoint f = T := sorry
  sorry
}

-- but we still have some issues remaining to address:
-- example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup_ofRing E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
--   [hE‚ÇÉ : Algebra ‚ÑÇ E] [hE‚ÇÑ : FiniteDimensional ‚ÑÇ E]
--   (T : E ‚Üí‚Çó[‚ÑÇ] E) (x y : E) :
--   ‚ü™T (x * y), T x‚ü´_‚ÑÇ = (LinearMap.adjoint (Algebra.linearMap ‚ÑÇ E)) x := sorry

/- The error is because of the following non-transparent diamond:
  the module (inferred by the inner product space) is not definitionally equal to that
  inferred by the algebra. -/
-- example {E : Type*} [NormedAddCommGroup_ofRing E]
--   [h : InnerProductSpace ‚ÑÇ E] [Algebra ‚ÑÇ E] :
--   h.toModule = Algebra.toModule :=
-- rfl

/--
 `Algebra ‚ÑÇ A` instance given `A` as a Semiring, `‚ÑÇ` Module, and
 has `SMulCommClass ‚ÑÇ A A` and `IsScalarTower ‚ÑÇ A A`
 (see implementation notes `Mathlib.Algebra.Algebra.Basic#implementation_notes`)
-/
instance {R A : Type*} [CommSemiring R] [Semiring A]
  [Module R A] [SMulCommClass R A A] [IsScalarTower R A A] : Algebra R A :=
Algebra.ofModule smul_mul_assoc mul_smul_comm

/-
 Now the module given by the inner product space **is** definitionally equal to that
   inferred by the algebra (which is given by the module of the inner product space).
-/
example {E : Type*} [NormedAddCommGroup_ofRing E]
  [h : InnerProductSpace ‚ÑÇ E] [SMulCommClass ‚ÑÇ E E]
  [IsScalarTower ‚ÑÇ E E] :
  h.toModule = Algebra.toModule :=
rfl

/- we have resolved all our diamonds! -/

-- ay this works now!!! üéâ
example {E : Type*} [hE‚ÇÅ : NormedAddCommGroup_ofRing E] [hE‚ÇÇ : InnerProductSpace ‚ÑÇ E]
  [SMulCommClass ‚ÑÇ E E] [IsScalarTower ‚ÑÇ E E] [FiniteDimensional ‚ÑÇ E]
  (T : E ‚Üí‚Çó[‚ÑÇ] E) (x y : E) :
  ‚ü™LinearMap.adjoint T (x * y), T x‚ü´_‚ÑÇ = (LinearMap.adjoint (Algebra.linearMap ‚ÑÇ E)) x := sorry

end resolving_diamonds
